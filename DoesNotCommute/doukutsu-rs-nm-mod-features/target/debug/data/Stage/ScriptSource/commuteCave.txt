//Flag List
//
//
//Flag 0500: on ONLY when quote is active
//Flag 0510: on ONLY when curly is active
//Flag 0520: on ONLY when gero is active
//Flag 0530: on ONLY when king is active
//
//
//


//time file list
//cave = first cave complete
//village = village complete
//corridor = corridor complete
//eggx = eggx complete
//oside = oside complete
//sci = science complete
//sciboss = securitron complete



//event range:
//$0100 = intro events, bound to the title screen
//$0200 = intro doors, each character gets 10 (210, 220, etc)
//$0300 = exit doors, each door gets 10, starting at 300, 399 is common wrong door script
//$0400 = 400 for arrow + any other ingame elements
//$0500 = fakechars, each gets 1, starting at 500, includes their initialization events




//stage entry event (titlescreen preview)
#0100
//initialize timer to 0 and then add 30 seconds to start
<SLT0001:$
<ADT0050:0000:0030
//give player timer so value persists
<EQ+0512
<HMC<FRE
<FAI0004
<WAI9999
<END

//selected "start game" in title menu
//or "esc" in stage select menu
#0101
<KEY<TRA0002:0100:0000:0000
<END

//left arrow
//#0102
//<END

//right arrow
#0103
<KEY<FAO0004
<TRA0005:0100:0000:0000
<END

//play current stage
#0104
//<TRA0001:0100:0015:0007//debug TRA
//send to start point
<TRA0003:0200:0010:0008
<END

//watch current stage
#0105
<END


//kick out (reserved for missing files)
#0110
<INI<END

//break blocks before fade-in
#0115
<CML0002:0057:0016:0000
<CML0002:0057:0017:0000
<CML0002:0057:0018:0000
<POP

//...199 is reserved


/////////////////INTROS/////////////////


//quote intro (lead-in map: it's a little different)
#0200
//timer already initialized
//IT+0000//add ITEMS
//<EQ+0000//add EQUIPS
//<AM+0000//add ARMS
<FAI0004<MNA<WAI0020
<ANP0320:0000:0002//DOOR
<SOU0011<WAI0010<SMC<WAI0010
<ANP0320:0000:0000<SOU0011<WAI0020//DOOR
<KEY<MS3Quote wakes up from an uneasy
nap.<NOD<CLO//PART 1 MESSAGE
<FON0330:0020<WAI0020//FOCUS ON e-DOOR
<CNP0400:0376:0003//config arrow
<CFG0400:0001:0058:0005//LOCATION
<CNP0401:0377:0000//config outline
<MNP0401:0058:0005:0000//LOCATION
<MSGFor reasons he doesn't feel
like saying, he wants to enter
the door on the top right.<NOD<CLO
<FOM0016<WAI0040//PART 2 MESSAGE
<EQ+0256//start timer + logging
<ALC0001:$
<END



//curly intro
#0210
<HMC<MOV0058:0005//MOVE TO START
<MYD0000//SET DIRECTION
//<IT+0000//add ITEMS
//<EQ+0000//add EQUIPS
<AM+0004:0100//add ARMS
<MIM0000:Curly$
<WAI0060
<ANP0330:0000:0002//DOOR
<SOU0011<WAI0010<SMC<WAI0010
<ANP0330:0000:0000<SOU0011<WAI0020//DOOR
<KEY<MSGCurly has quite a bit
to do.<NOD<CLO//PART 1 MESSAGE
<FON0310:0020<WAI0020//FOCUS ON e-DOOR
<CNP0400:0376:0003//config arrow
<CFG0400:0001:0002:0024//LOCATION
<CNP0401:0377:0000//config outline
<MNP0401:0058:0002:0024//LOCATION
<MSGBecause of this, she must waste no
time and get to the door
on the bottom left.<NOD<CLO//PART 2 MESSAGE
<FOM0016<WAI0040
<PSH0500//START NPCs
<EQ+0256//start timer + logging
<ALC0001:$
<END

//gero
#0220
<HMC<MOV0003:0004//MOVE TO START
<MYD0002//SET DIRECTION
<IT+0016//add ITEMS
//<EQ+0000//add EQUIPS
//<AM+0004:0100//add ARMS
<MIM0000:Gero$
<WAI0060
<ANP0300:0000:0002//DOOR
<SOU0011<WAI0010<SMC<WAI0010
<ANP0300:0000:0000<SOU0011<WAI0020//DOOR
<KEY<MSG<MSGIt's Dr Gero!<NOD<CLRHe's been called across the Island
for a medical emergency.<NOD<CLRHe was so rushed,
he forgot to grab any weapons.<NOD<CLRAll he has in his posession
is a<GIT1016 single dose of Cure-All.<NOD<GIT0000<CLO//PART 1 MESSAGE
<FON0340:0020<WAI0020//FOCUS ON e-DOOR
<CNP0400:0376:0003//config arrow
<CFG0400:0001:0055:0010//LOCATION
<CNP0401:0377:0000//config outline
<MNP0401:0055:0010:0000//LOCATION
<MSGAs fast as he can,
he needs to get to the door
on the right side.<NOD<CLO//PART 2 MESSAGE
<FOM0016<WAI0040
<PSH0500//START NPCs
<PSH0510
<EQ+0256//start timer + logging
<ALC0001:$
<END

//king
#0230
<HMC<MOV0055:0010//MOVE TO START
<MYD0000//SET DIRECTION
//<IT+0016//add ITEMS
//<EQ+0000//add EQUIPS
<AM+0009:0000//add ARMS
<MIM0000:King$
<WAI0060
<ANP0340:0000:0002//DOOR
<SOU0011<WAI0010<SMC<WAI0010
<ANP0340:0000:0000<SOU0011<WAI0020//DOOR
<KEY<MSGKing totes a sharp sword.<NOD<CLO//PART 1 MESSAGE
<FON0351:0020<WAI0020//FOCUS ON e-DOOR
<CNP0400:0376:0000//config arrow
<CFG0400:0001:0063:0017//LOCATION
//<CNP0401:0377:0000//config outline
//<MNP0401:0058:0063:0017//LOCATION
<MSGFresh from
laying down the law, he wants
to return home to the village.<NOD<CLO//PART 2 MESSAGE
<FOM0016<WAI0040
<PSH0500//START NPCs
<PSH0510
<PSH0520
<EQ+0256//start timer + logging
<ALC0001:$
<END

/////////////////DOORS/////////////////

//top left (nobody)
#0300

//mid left (curly)
#0310
//stop logging and save to file
<FNJ0510:0399//PLAYER'S FLAG
<EQ-0256//stop timer
<ALC0000:Cave2$
<CNP0400:0000:0000<CNP0401:0000:0000//hide indicators
<KEY<ANP0310:0000:0002<SOU0011<WAI0020//open DOOR
<HMC
<ANP0310:0000:0000<SOU0011<WAI0060//close DOOR
//<IT-0000//de-equip ITEMS
//<EQ-0000//de-equip equips
<AM-0004//de-equip WEAPONS
<FL-0510//PLAYER'S FLAG
<FL+0520//NEXT PLAYER FLAG
<TRA0004:0220:0002:0024//TO NEXT DOOR

//lower left (nobody)
#0320

//top right (quote)
#0330
//stop logging and save to file
<FNJ0500:0399//PLAYER'S FLAG
<EQ-0256//stop timer
<ALC0000:Cave1$
<CNP0400:0000:0000<CNP0401:0000:0000//hide indicators
<KEY<ANP0330:0000:0002<SOU0011<WAI0020//open DOOR
<HMC
<ANP0330:0000:0000<SOU0011<WAI0060//close DOOR
//<IT-0000//de-equip ITEMS
//<EQ-0000//de-equip equips
<AM-0002//de-equip WEAPONS
<FL-0500//PLAYER'S FLAG
<FL+0510//NEXT PLAYER FLAG
<TRA0004:0210:0058:0005//TO NEXT DOOR (USE CURR COORDS)




//mid right (gero)
#0340
//stop logging and save to file
<FNJ0520:0399//PLAYER'S FLAG
<EQ-0256//stop timer
<ALC0000:Cave3$
<CNP0400:0000:0000<CNP0401:0000:0000//hide indicators
<KEY<ANP0340:0000:0002<SOU0011<WAI0020//open DOOR
<HMC
<ANP0340:0000:0000<SOU0011<WAI0060//close DOOR
<IT-0016//de-equip ITEMS
//<EQ-0000//de-equip equips
//<AM-0002//de-equip WEAPONS
<FL-0520//PLAYER'S FLAG
<FL+0530//NEXT PLAYER FLAG
<TRA0004:0230:0055:0010//TO NEXT DOOR (USE CURR COORDS)


//h/v trigger (king)
#0350
<EQ-0256//stop timer
<ALC0000:Cave4$


//focus target
#0351

//Wrong door
#0399
<PRI<MSGThis is not your door!<NOD<END


/////////////////ITEMS/////////////////

//arrow
#0400
<END

//door outline
#0401
<END


/////////////////FAKECHARS/////////////////

//quote NPC (+fresh load)
#0500
<NIM0500:Quote$
<ARL0500:Cave1$
<ANP0500:0001:0000//start playback
<POP<END
#0501
<CFG0500:0003:0000:0000//rewind
<ANP0500:0001:0000//start playback
<POP<END

//curly NPC
#0510
<NIM0510:Curly$
<ARL0510:Cave2$
<ANP0510:0001:0000//start playback
<POP<END
#0511
<CFG0510:0003:0000:0000//rewind
<ANP0510:0001:0000//start playback
<POP<END

//gero NPC
#0520
<NIM0500:Gero$
<ARL0500:Cave3$
<ANP0500:0001:0000//start playback
<POP<END
#0521
<CFG0510:0003:0000:0000//rewind
<ANP0510:0001:0000//start playback
<POP<END

//king NPC
#0530
<NIM0510:King$
<ARL0510:Cave4$
<ANP0510:0001:0000//start playback
<POP<END
#0531
<CFG0510:0003:0000:0000//rewind
<ANP0510:0001:0000//start playback
<POP<END
























